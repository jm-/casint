Cls();
ViewWindow(1, 127, 0, 63, 1, 0);

D = 10;  // spacing
N = 5;  // number of letters
G = 6;  // number of guesses

// draw keyboard
for (L = 1 to 26)
{
    O = 5; Prog("WORDLSUB");
    O = 3; Prog("WORDLSUB");
}

dim Mat G = (3, 5);

// choose a word
O = 0; Prog("WORDLSUB");
DebugVar(W);

// extract the solution word
O = 2; Prog("WORDLSUB");

// main loop
A = 1;  // guess
B = 1;  // letter
while (A <= G)
{
    DebugMat(G);
    X = B * D - 6;
    Y = A * D - 6;
    if (B <= N)
    {
        Text(Y + 6, X, "^");
    }
    
    do { K = GetKey(); } while (K == 0);

    Text(Y + 6, X, "  ");
    
    if (K == 31)
    {
        if (B > N)
        {
            // score the guess
            O = 4; Prog("WORDLSUB");
            if (S == 10) { break; }     // win condition
            A = A + 1;
            B = 1;
        }
    }
    else
    {
        if (K == 44)
        {
            if (B > 1)
            {
                B = B - 1;
                X = B * D - 6;
                Text(Y, X, "  ");
            }
        }
        else
        {
            if (B <= N)
            {
                O = 1; Prog("WORDLSUB");
                if (L > 0)
                {
                    Mat G [2, B] = L;
                    O = 3; Prog("WORDLSUB");
                    B = B + 1;
                }
            }
        }
    }
}

O = 6; Prog("WORDLSUB");
if (S == 10)
{
    if (A == 1) { Text(31, 53, "Genius"); }
    if (A == 2) { Text(31, 43, "Magnificent"); }
    if (A == 3) { Text(31, 44, "Impressive"); }
    if (A == 4) { Text(31, 49, "Splendid"); }
    if (A == 5) { Text(31, 54, "Great"); }
    if (A == 6) { Text(31, 56, "Phew"); }
}
else
{
    Y = 27;
    O = 3;
    for (B = 1 to 5)
    {
        L = Mat G [1, B];
        X = B * D + 31;
        Prog("WORDLSUB");
    }
    Text(34, 53, "Oh No");
}
